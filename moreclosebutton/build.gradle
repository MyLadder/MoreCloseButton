apply plugin: 'com.android.library'
apply plugin: 'kotlin-android'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'com.github.dcendents.android-maven'

//Bintray depends on this global variable to set the library version!
version = '0.9.4'
group = 'br.com.simplepass'

android {
    compileSdkVersion 25
    buildToolsVersion "25.0.0"

    defaultConfig {
        minSdkVersion 16
        targetSdkVersion 25
        versionCode 1
        versionName "1.0"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }
    sourceSets {
        main.java.srcDirs += 'src/main/kotlin'
    }

    lintOptions {
        abortOnError false
    }
}

install {
    repositories.mavenInstaller {
        pom.project {
            name 'MoreCloseButton'
            description 'A library for a custom button'
            url 'https://github.com/leandroBorgesFerreira/MoreCLoseButton'
            inceptionYear '2016'

            packaging 'aar'
            groupId 'br.com.simplepass'
            artifactId 'moreclosebutton'
            version '0.9.4'

            licenses {
                license {
                    name 'The Apache Software License, Version 2.0'
                    url 'http://www.apache.org/licenses/LICENSE-2.0.txt'
                    distribution 'repo'
                }
            }
            scm {
                connection 'https://github.com/leandroBorgesFerreira/MoreCLoseButton.git'
                url 'https://github.com/leandroBorgesFerreira/MoreCLoseButton'

            }
            developers {
                developer {
                    name 'Leandro Borges Ferreira'
                }
            }
        }
    }
}

// Configure gradle-bintray-plugin (for publishing releases)
bintray {
    configurations = ['archives']

    publish = true

    pkg {
        repo = 'maven'
        name = 'MoreCloseButton'
        userOrg = 'lehen01'
        licenses = ['Apache-2.0']
        labels = ['customview', 'animation']
        websiteUrl = 'https://github.com/leandroBorgesFerreira/MoreCLoseButton'
        issueTrackerUrl = 'https://github.com/leandroBorgesFerreira/MoreCLoseButton/issues'
        vcsUrl = 'https://github.com/leandroBorgesFerreira/MoreCLoseButton.git'
        publicDownloadNumbers = true
    }

}

if (project.hasProperty('bintrayUser') && project.hasProperty('bintrayKey')) {
    bintray.user = project.bintrayUser
    bintray.key = project.bintrayKey
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    compile 'com.android.support:appcompat-v7:25.0.1'
    compile "org.jetbrains.kotlin:kotlin-stdlib:$kotlin_version"
}

repositories {
    mavenCentral()
}

//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/bintrayv1.gradle'
//apply from: 'https://raw.githubusercontent.com/nuuneoi/JCenter/master/installv1.gradle'
